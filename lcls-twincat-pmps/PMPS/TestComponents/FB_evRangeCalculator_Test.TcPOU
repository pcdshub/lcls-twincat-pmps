<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_evRangeCalculator_Test" Id="{e6bcc6ca-553c-488f-ba3b-eabce151236e}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'call_after_init'}
FUNCTION_BLOCK FB_evRangeCalculator_Test EXTENDS FB_TestSuite
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
END_VAR
VAR CONSTANT
    RandomBoundary : INT := 2;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[BasicFunction();]]></ST>
    </Implementation>
    <Method Name="BasicFunction" Id="{2787ce06-fe9f-4a43-87a2-e050fc9f0a9f}">
      <Declaration><![CDATA[{attribute 'no_check'}
METHOD BasicFunction
VAR_INPUT
END_VAR
VAR
    {attribute 'displaymode' := 'binary'}
    LastWord : DWORD := 2#0000_0000_0000_0000_0000_0000_0000_0010;
    {attribute 'displaymode' := 'binary'}
    Result : DWORD;
    {attribute 'displaymode' := 'binary'}
    TestEV : DWORD;

    {attribute 'displaymode' := 'binary'}
    tEvW : DWORD;
    tEv : REAL;


    fbStr : FB_FormatString;

    MiscNumber : INT;
END_VAR
VAR CONSTANT
    nLowerLimitBoundary : INT := 0;

    RandomBoundary : INT := 2;
    MNU : INT := 3; //RandomBoundary + 1
    MND : INT := 1; //RandomBoundary - 1
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Test('CheckRanges');

//Lower limit equal
tEvW.nLowerLimitBoundary := TRUE;
tEv := PMPS_GVL.g_areVBoundaries[nLowerLimitBoundary];
Result := F_eVRangeCalculator(tEv, 0);
AssertTrue(
    Result = tEvW,
    'Lower limit not inclusive');

// Lower range flags
tEvW.nLowerLimitBoundary := TRUE;
tEv := PMPS_GVL.g_areVBoundaries[nLowerLimitBoundary] / 2;
Result := F_eVRangeCalculator(tEv, 0);
AssertTrue(
    Result = tEvW,
    'eV below lower range should come back true');

//Mid range
tEvW := 0;
tEvW.RandomBoundary := TRUE;
//ev result that is smack dab in the middle of a range
tEv := PMPS_GVL.g_areVBoundaries[RandomBoundary] - ((PMPS_GVL.g_areVBoundaries[RandomBoundary] - PMPS_GVL.g_areVBoundaries[MND]) / 2);
Result := F_eVRangeCalculator(tEv, 0);
AssertTrue(
    Result = tEvW,
    'In range failed, whatever you did, it broke this really bad.');

//Out of range
tEvW := 0;
tEvW.RandomBoundary := TRUE;
//Ev result that is beyond the current range (midway into the next range up)
tEv := PMPS_GVL.g_areVBoundaries[MNU] - (PMPS_GVL.g_areVBoundaries[MNU]-PMPS_GVL.g_areVBoundaries[RandomBoundary])/2;
Result := F_eVRangeCalculator(tEv, 0);
AssertFalse(
    Result = tEvW,
    'Out of range failed, whatever you did, it broke this really bad.');

TEST_FINISHED();

Test('Negative eV failsafes');
//Negative eV
AssertTrue(
    F_eVRangeCalculator(-300, 0) = 16#FFFF_FFFF,
    'Working with antimatter? Negative eV should failsafe to FFFF.');

TEST_FINISHED();

Test('Out of range eV failsafes');
// eV too high
AssertTrue(
    F_eVRangeCalculator(PMPS_GVL.g_areVBoundaries[PMPS_GVL.g_cBoundaries] + 1, 0) = 16#FFFF_FFFF,
    'eV above the last threshold should failsafe.');

TEST_FINISHED();

Test('Check hyst keeps');

LastWord := F_eVRangeCalculator(PMPS_GVL.g_areVBoundaries[RandomBoundary], 2#0000_0000_0000_0000_0000_0000_0000_0000);

// Last word should be 2#0000_0000_0000_0100

    Result := F_eVRangeCalculator(
        PMPS_GVL.g_areVBoundaries[RandomBoundary] + PMPS_GVL.reVHyst - PMPS_GVL.reVHyst/2,
         LastWord);
    TestEV := 0;
    TestEV.RandomBoundary := TRUE;
    TestEV.MNU := True;

    fbStr.arg1 := F_DWORD(Result);
    fbStr.arg2 := F_DWORD(TestEV);
    fbStr.sFormat := 'Moving up does not stick. Result: %b vs. Test: %b';
    fbStr();

AssertTrue(Result = TestEV,
    fbStr.sOut);

    Result := F_eVRangeCalculator(
        PMPS_GVL.g_areVBoundaries[RandomBoundary - 1] - PMPS_GVL.reVHyst + PMPS_GVL.reVHyst/2,
        LastWord);
    TestEV := 0;
    TestEV.RandomBoundary := TRUE;
    TestEV.MND := True;

    fbStr.arg1 := F_DWORD(Result);
    fbStr.arg2 := F_DWORD(TestEV);
    fbStr.sFormat := 'Moving down does not stick. Result: %b vs. Test: %b';
    fbStr();

AssertTrue(Result = TestEV,
    fbStr.sOut);

TEST_FINISHED();

Test('Check hyst drops');

    Result := F_eVRangeCalculator(
        PMPS_GVL.g_areVBoundaries[MND] - PMPS_GVL.reVHyst*1.1,
        LastWord);
    TestEV := 0;
    TestEV.MND := TRUE;

    fbStr.arg1 := F_DWORD(Result);
    fbStr.arg2 := F_DWORD(TestEV);
    fbStr.sFormat := 'Moving down does not drop. Result: %b vs. Test: %b';
    fbStr();

AssertTrue(Result = TestEV,
    fbStr.sOut);

    Result := F_eVRangeCalculator(
        PMPS_GVL.g_areVBoundaries[RandomBoundary] + PMPS_GVL.reVHyst*1.1,
        LastWord);
    TestEV := 0;
    TestEV.MNU := TRUE;

    fbStr.arg1 := F_DWORD(Result);
    fbStr.arg2 := F_DWORD(TestEV);
    fbStr.sFormat := 'Moving up does not drop. Result: %b vs. Test: %b';
    fbStr();

AssertTrue(Result = TestEV,
    fbStr.sOut);

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>