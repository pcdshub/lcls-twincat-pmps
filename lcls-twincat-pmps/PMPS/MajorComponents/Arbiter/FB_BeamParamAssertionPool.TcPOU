<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_BeamParamAssertionPool" Id="{f4decfdf-ca0a-4e3b-aae3-b33f686ae70e}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function block implements simple database. Data element values are stored in the hash table.  *)
{attribute 'no_check'}
FUNCTION_BLOCK FB_BeamParamAssertionPool
VAR_INPUT
    key         : DWORD := 0;(* Entry key: used by A_Lookup, A_Remove method, the key variable is also used by A_Add method *)
    putPosPtr    : POINTER TO T_HashTableEntry := 0;(* Hash table entry position pointer (used by A_Find, A_GetNext, A_GetPrev) *)
    putValue     : ST_BP_ArbInternal;(* Hash table entry value (used by A_AddHead, A_AddTail, A_Find )*)
END_VAR
VAR_OUTPUT
    bOk            : BOOL := FALSE;(* TRUE = Success, FALSE = Failed *)
    getPosPtr    : POINTER TO T_HashTableEntry := 0;(* Returned hash table entry position pointer *)
    getValue     : ST_BP_ArbInternal;(* Returned hash table entry value *)
    nCount        : UDINT := 0;(* Hash table size (number of used entries, used by A_Count) *)
END_VAR
VAR
    {attribute 'pytmc' := '
        pv: Entry
        io: i
     '}
    epicsDataPool    : ARRAY[1..PMPS_PARAM.MAX_ASSERTIONS] OF ST_BP_ArbInternal;(* Structured data element pool for display in EPICS*)
    dataPool    : ARRAY[0..PMPS_PARAM.MAX_ASSERTIONS*3] OF ST_BP_ArbInternal;(* Structured data element pool *)
    entries        : ARRAY[0..PMPS_PARAM.MAX_ASSERTIONS*3] OF T_HashTableEntry;(* Max. number of hash table entries. The value of table entry = 32 bit integer (pointer to dataPool-array-entry) *)
    fbTable     : FB_HashTableCtrl;(* basic hash table control function block *)
    hTable         : T_HHASHTABLE;(* hash table handle *)
    pRefPtr        : POINTER TO ST_BP_ArbInternal := 0;
    indexOfElem    : ULINT;(* Integer value (max. size: x86=>32bit, x64=>64bit)*)
    cstSafeBeam :ST_BeamParams := ( nTran := 0, neVRange := 0, nRate  := 0, nBCRange :=0);// MG
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[;]]></ST>
    </Implementation>
    <Action Name="A_Add" Id="{91bc4edd-0e3d-4fad-8117-353f2c55b3fc}">
      <Implementation>
        <ST><![CDATA[(* Adds entry to the table *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_Add( hTable := hTable, key := key, putValue := ADR(cstSafeBeam)(* we will set this value later *), getPosPtr=>getPosPtr, bOk=>bOk );(* Add new element to the table, getPosPtr points to the new entry *)
IF fbTable.bOk THEN(* Success *)
    fbTable.A_GetIndexAtPosPtr( hTable := hTable, putPosPtr := getPosPtr, getValue =>indexOfElem, bOk=>bOk );(* Get array index of getPosPtr entry *)
    IF fbTable.bOk THEN(* Success *)
        pRefPtr     := ADR( dataPool[indexOfElem] );(* Get pointer to the data element *)

        pRefPtr^ := putValue;(* copy application value *)

        fbTable.A_Add( hTable := hTable, key := key, putValue := pRefPtr, bOk=>bOk );(* Assign the entry value = pointer to the data element *)
        IF fbTable.bOk THEN(* Success *)
            getValue := putValue;
        END_IF
    END_IF
END_IF

DataPoolToEpics();]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Count" Id="{b3cd4932-0eef-4632-ae8a-9606246784ff}">
      <Implementation>
        <ST><![CDATA[(* Count number of used entries *)
nCount := hTable.nCount;
bOk := TRUE;]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_GetFirst" Id="{2654f312-636a-4578-b772-084bd5aa9434}">
      <Implementation>
        <ST><![CDATA[(* Get first entry position pointer and value *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_GetFirst( hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
    getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_GetNext" Id="{8f6924c4-1ee3-488c-ab09-875d6d1158bf}">
      <Implementation>
        <ST><![CDATA[(* Get next entry position pointer and value *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;
bOk := FALSE;

IF putPosPtr = 0 THEN
    RETURN;
END_IF

fbTable.A_GetNext( hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
    getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Lookup" Id="{bcb8b1cb-24a5-4138-aaed-7deabb1c8c77}">
      <Implementation>
        <ST><![CDATA[(* Lookup for entry by key  *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_Lookup( key := key, hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
    getValue := pRefPtr^;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Remove" Id="{6e4c3257-3324-49a9-bb39-de42238cd217}">
      <Implementation>
        <ST><![CDATA[(* Search for entry and remove it *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;

fbTable.A_Remove( key := key, hTable := hTable, putPosPtr := putPosPtr, getValue=>pRefPtr, getPosPtr=>getPosPtr, bOk=>bOk );
IF fbTable.bOk THEN
    getValue := pRefPtr^;
END_IF

DataPoolToEpics();]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Reset" Id="{2fad12cc-675c-478a-ba51-13f1caa560b0}">
      <Implementation>
        <ST><![CDATA[(* Reset/initialize linked list *)
MEMSET( ADR( getValue ), 0, SIZEOF( getValue ) );
getPosPtr := 0;
bOk := F_CreateHashTableHnd( ADR( entries ), SIZEOF( entries ), hTable );(* Intialize table handle *)
fbTable.A_Reset( hTable := hTable, bOk=>bOk );
nCount := 0;]]></ST>
      </Implementation>
    </Action>
    <Action Name="DataPoolToEpics" Id="{47946703-b251-42a6-b725-16ba7f72b5bf}">
      <Implementation>
        <ST><![CDATA[
MEMCPY(ADR(epicsDataPool), ADR(dataPool), (PMPS_PARAM.MAX_ASSERTIONS) * SIZEOF(ST_BP_ArbInternal));]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>